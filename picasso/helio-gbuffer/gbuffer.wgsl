enable wgpu_binding_array;

//! G-buffer write pass (GPU-driven).
//!
//! Rasterises scene geometry into screen-sized textures:
//!   target 0 – albedo    (Rgba8Unorm)
//!   target 1 – normal    (Rgba16Float)
//!   target 2 – orm       (Rgba8Unorm)
//!   target 3 – emissive  (Rgba16Float)
//!   target 7 – velocity  (Rg16Float)  — screen-space motion in pixels/frame
//!
//! Resolved F0 is packed into unused alpha channels:
//!   normal.a   = F0.r
//!   orm.a      = F0.g
//!   emissive.a = F0.b

struct Camera {
    view:           mat4x4<f32>,
    proj:           mat4x4<f32>,
    view_proj:      mat4x4<f32>,
    view_proj_inv:  mat4x4<f32>,
    position_near:  vec4<f32>,
    forward_far:    vec4<f32>,
    jitter_frame:   vec4<f32>,
    prev_view_proj: mat4x4<f32>,
}

struct Globals {
    frame: u32,
    delta_time: f32,
    light_count: u32,
    ambient_intensity: f32,
    ambient_color: vec4<f32>,
    rc_world_min: vec4<f32>,
    rc_world_max: vec4<f32>,
    csm_splits: vec4<f32>,
    debug_mode: u32,
    screen_width: f32,
    screen_height: f32,
    _pad0: u32,
}

/// GPU material (96 bytes, matches libhelio::GpuMaterial)
struct GpuMaterial {
    base_color:         vec4<f32>,
    emissive:           vec4<f32>,
    roughness_metallic: vec4<f32>,
    tex_base_color:     u32,
    tex_normal:         u32,
    tex_roughness:      u32,
    tex_emissive:       u32,
    tex_occlusion:      u32,
    workflow:           u32,
    flags:              u32,
    material_class:     u32,
    class_params:       vec4<f32>,
}

const FLAG_HAS_NORMAL_MAP: u32 = 1u << 3u;
const FLAG_HAS_CLEAR_COAT: u32 = 1u << 4u;
const FLAG_HAS_SUBSURFACE: u32 = 1u << 5u;
const FLAG_HAS_ANISOTROPY: u32 = 1u << 6u;

const SURFACE_FLAG_SUBSURFACE: u32 = 1u << 0u;
const SURFACE_FLAG_ANISOTROPIC: u32 = 1u << 1u;
const SURFACE_FLAG_LOW_SPECULAR: u32 = 1u << 2u;

/// Per-material texture metadata (224 bytes, matches helio::GpuMaterialTextures)
struct MaterialTextureSlot {
    texture_index: u32,
    uv_channel:    u32,
    _pad0:         u32,
    _pad1:         u32,
    offset_scale:  vec4<f32>,
    rotation:      vec4<f32>,
}

struct MaterialTextureData {
    base_color:         MaterialTextureSlot,
    normal:             MaterialTextureSlot,
    roughness_metallic: MaterialTextureSlot,
    emissive:           MaterialTextureSlot,
    occlusion:          MaterialTextureSlot,
    specular_color:     MaterialTextureSlot,
    specular_weight:    MaterialTextureSlot,
    params:             vec4<f32>,  // x=normal_scale, y=occlusion_strength, z=alpha_cutoff
}

struct SceneObjectSpatial {
    transform:    mat4x4<f32>,
    normal_mat_0: vec4<f32>,
    normal_mat_1: vec4<f32>,
    normal_mat_2: vec4<f32>,
    sphere:       vec4<f32>,
    flags:        u32,
    _pad0:        u32,
    _pad1:        u32,
    _pad2:        u32,
}

struct SceneObjectRender {
    mesh_row:      u32,
    material_row:  u32,
    lightmap_index: u32,
    _reserved:     u32,
}

struct ObjectHistory {
    transform: mat4x4<f32>,
    sphere:    vec4<f32>,
    flags:     u32,
    _pad0:     u32,
    _pad1:     u32,
    _pad2:     u32,
}

/// Lightmap atlas region for a mesh (32 bytes).
///
/// uv_clamp_min/max are precomputed half-texel-inset bounds that prevent bilinear
/// filtering from bleeding across neighbouring atlas region boundaries at runtime.
struct LightmapAtlasRegion {
    uv_offset:    vec2<f32>,  // Top-left corner in atlas [0,1] space
    uv_scale:     vec2<f32>,  // Extent in atlas [0,1] space
    uv_clamp_min: vec2<f32>,  // uv_offset + 0.5/atlas_size  (half-texel inner inset)
    uv_clamp_max: vec2<f32>,  // uv_offset + uv_scale - 0.5/atlas_size
}

@group(0) @binding(0) var<storage, read> cameras: array<Camera, 2>;
@group(0) @binding(1) var<uniform>          globals:                Globals;
@group(0) @binding(2) var<storage, read>    object_spatial:         array<SceneObjectSpatial>;
@group(0) @binding(3) var<storage, read>    lightmap_atlas_regions: array<LightmapAtlasRegion>;
// Per-draw-call-group compacted original instance slots, surviving both
// frustum culling (IndirectDispatchPass) and Hi-Z occlusion culling
// (OcclusionCullPass). `instance_index` on an indirect draw ranges over the
// group's now-possibly-smaller compacted count, so it must be redirected
// through this buffer before indexing `instance_data` — it no longer equals
// the instance's real slot directly.
@group(0) @binding(4) var<storage, read>    compacted_indices:      array<u32>;
// Coordinate-space transforms — see `libhelio::{coordinate_space, set_coordinate_space}`.
// Slot 0 is always identity, so an untagged instance (the common case) is
// unaffected beyond one extra constant-buffer read + mat4x4 multiply.
// Sublevels and portals both work by tagging instances/draws with a non-zero
// slot here and moving the whole space with a single matrix write.
@group(0) @binding(5) var<storage, read>    coordinate_spaces:      array<mat4x4<f32>>;
@group(0) @binding(6) var<storage, read>    coordinate_spaces_prev: array<mat4x4<f32>>;
@group(0) @binding(7) var<storage, read>    object_render:          array<SceneObjectRender>;
@group(0) @binding(8) var<storage, read>    object_history:         array<ObjectHistory>;

@group(1) @binding(0) var<storage, read>    materials:          array<GpuMaterial>;
@group(1) @binding(1) var<storage, read>    material_textures:  array<MaterialTextureData>;
@group(1) @binding(2) var                   scene_textures:     binding_array<texture_2d<f32>, 256>;
@group(1) @binding(3) var                   scene_samplers:     binding_array<sampler, 256>;

struct Vertex {
    @location(0) position:       vec3<f32>,
    @location(1) bitangent_sign: f32,
    @location(2) tex_coords:     vec2<f32>,  // UV0 — material/albedo channel (may tile)
    @location(3) normal:         u32,
    @location(4) tangent:        u32,
    @location(5) lightmap_uv:    vec2<f32>,  // UV1 — dedicated lightmap channel, non-overlapping [0,1]
}

struct VertexOutput {
    @invariant @builtin(position) clip_position: vec4<f32>,
    @location(0) world_position:     vec3<f32>,
    @location(1) world_normal:       vec3<f32>,
    @location(2) tex_coords:         vec2<f32>,
    @location(3) world_tangent:      vec3<f32>,
    @location(4) bitangent_sign:     f32,
    @location(5) @interpolate(flat) material_id:    u32,
    @location(6) lightmap_uv:        vec2<f32>,  // Lightmap atlas UV (or (0,0) if no lightmap)
    @location(7) prev_clip_position: vec4<f32>,  // Previous frame clip-space position (velocity)
}

fn decode_snorm8x4(packed: u32) -> vec3<f32> {
    return unpack4x8snorm(packed).xyz;
}

@vertex
fn vs_main(v: Vertex, @builtin(instance_index) slot: u32) -> VertexOutput {
    let entity_row = compacted_indices[slot];
    let inst       = object_spatial[entity_row];
    let render     = object_render[entity_row];
    let history    = object_history[entity_row];

    // Coordinate space: world space (slot 0, identity) for the overwhelming
    // common case, or wherever a sublevel/portal has placed this instance.
    // See the `coordinate_spaces` binding above.
    let space_id   = (inst.flags >> 8u) & 0xFFu;
    let space      = coordinate_spaces[space_id];
    let prev_space_id = (history.flags >> 8u) & 0xFFu;
    let space_prev = coordinate_spaces_prev[prev_space_id];
    let space_rot  = mat3x3<f32>(space[0].xyz, space[1].xyz, space[2].xyz);

    let world_pos  = space * (inst.transform * vec4<f32>(v.position, 1.0));

    // Normals transform by the inverse-transpose (stored in normal_mat), then
    // by the coordinate space's rotation. Coordinate spaces are always rigid
    // (translation + rotation, never scaled), so `space_rot` is its own
    // inverse-transpose and needs no separate normal-matrix treatment.
    let normal_mat = space_rot * mat3x3<f32>(
        inst.normal_mat_0.xyz,
        inst.normal_mat_1.xyz,
        inst.normal_mat_2.xyz,
    );

    // Tangents are NOT normals — they transform by the regular upper-3×3 of
    // the model matrix (no inverse-transpose), then the same rigid rotation.
    let model_mat3 = space_rot * mat3x3<f32>(
        inst.transform[0].xyz,
        inst.transform[1].xyz,
        inst.transform[2].xyz,
    );

    // Previous-frame clip position for velocity buffer. Uses the coordinate
    // space's own previous-frame transform so a moving sublevel/portal still
    // produces correct motion vectors instead of a false one-frame pop.
    let prev_world  = space_prev * (history.transform * vec4<f32>(v.position, 1.0));
    let prev_clip   = cameras[0].prev_view_proj * prev_world;

    var out: VertexOutput;
    out.clip_position      = cameras[0].view_proj * world_pos;
    out.world_position     = world_pos.xyz;
    out.world_normal       = normalize(normal_mat  * decode_snorm8x4(v.normal));
    out.world_tangent      = normalize(model_mat3  * decode_snorm8x4(v.tangent));
    out.bitangent_sign     = v.bitangent_sign;
    out.tex_coords         = v.tex_coords;
    out.material_id        = render.material_row;
    out.prev_clip_position = prev_clip;
    
    // Compute lightmap UV from atlas region.
    //
    // UV CHANNEL SELECTION STRATEGY
    // ──────────────────────────────
    // If the mesh has a dedicated lightmap UV channel (UV1, non-zero), use it.
    // UV1 is artist-authored or tool-generated to be non-overlapping and in [0,1],
    // exactly what offline bakers need. Nebula receives UV1 explicitly via
    // `lightmap_uvs: Some(...)` in mesh_upload_to_bake when UV1 is non-trivial.
    //
    // If UV1 is all-zero (mesh has only one UV channel), fall back to UV0
    // clamped to [0,1].  UV0 is what Nebula baked with in that case
    // (mesh_upload_to_bake passes UV0 as lightmap_uvs when UV1 is absent).
    // Clamping prevents tiled UV0 values (e.g. 2.3) from mapping outside
    // the atlas region and hitting neighbouring meshes' texels (the original
    // "random dim slivers" bug).
    //
    // The computed atlas UV is then half-texel-inset clamped to [uv_clamp_min,
    // uv_clamp_max] to prevent bilinear filtering from bleeding across atlas
    // region boundaries regardless of which UV channel was chosen.
    let lightmap_idx = render.lightmap_index;
    if lightmap_idx != 0xFFFFFFFFu {
        let region = lightmap_atlas_regions[lightmap_idx];
        // Use UV1 if any component is clearly non-zero; otherwise fall back to UV0.
        let use_uv1 = any(abs(v.lightmap_uv) > vec2<f32>(0.001));
        let lm_input = select(
            clamp(v.tex_coords, vec2<f32>(0.0), vec2<f32>(1.0)),  // UV0 path: clamp to [0,1]
            v.lightmap_uv,                                           // UV1 path: already in [0,1]
            use_uv1,
        );
        let raw_uv = region.uv_offset + lm_input * region.uv_scale;
        out.lightmap_uv = clamp(raw_uv, region.uv_clamp_min, region.uv_clamp_max);
    } else {
        // Sentinel: negative UV signals "no lightmap" to the deferred pass.
        // Cannot use (0,0) because a valid atlas region can start at (0,0).
        out.lightmap_uv = vec2<f32>(-1.0, -1.0);
    }
    return out;
}

// ── Fragment ─────────────────────────────────────────────────────────────────

struct GBufferOutput {
    @location(0) albedo:      vec4<f32>,
    @location(1) normal:      vec4<f32>,
    @location(2) orm:         vec4<f32>,
    @location(3) emissive:    vec4<f32>,
    @location(4) lightmap_uv: vec2<f32>,
    @location(5) sss:         vec4<f32>,
    @location(6) extra:       vec4<f32>,
    @location(7) velocity:    vec2<f32>,  // screen-space motion in pixels/frame
}

// ── Surface data passed to GBuffer packing ──────────────────────────────────

struct SurfaceData {
    albedo:              vec4<f32>,
    normal:              vec3<f32>,
    ao:                  f32,
    roughness:           f32,
    metallic:            f32,
    specular_f0:         vec3<f32>,
    emissive:            vec3<f32>,
    alpha:               f32,
    flags:               u32,
    subsurface_color:    vec3<f32>,
    subsurface_radius:   f32,
    roughness_aniso_x:   f32,
    roughness_aniso_y:   f32,
    aniso_rotation:      f32,
}

const NO_TEXTURE: u32 = 0xffffffffu;
const MATERIAL_WORKFLOW_METALLIC: u32 = 0u;
const MATERIAL_WORKFLOW_SPECULAR: u32 = 1u;

/// High-bit opt-in for atlas regions whose source UVs intentionally tile beyond
/// 0..1.  The lower bits remain available for a real UV-channel selector when
/// meshes expose more than UV0.
const UV_WRAP_BEFORE_TRANSFORM: u32 = 0x80000000u;

/// Select UV channel and apply texture transform
fn select_uv(slot: MaterialTextureSlot, base_uv: vec2<f32>) -> vec2<f32> {
    // TODO: support uv_channel when we have tex_coords1
    // An atlas transform must operate on a single tiled source cell. Without
    // this opt-in wrap, a greedy voxel quad with UVs 0..N walks into adjacent
    // atlas tiles instead of repeating its own block texture.
    var source_uv = base_uv;
    if (slot.uv_channel & UV_WRAP_BEFORE_TRANSFORM) != 0u {
        source_uv = fract(base_uv);
    }
    let scaled = source_uv * slot.offset_scale.zw;
    let s = slot.rotation.x;
    let c = slot.rotation.y;
    let rotated = vec2<f32>(
        scaled.x * c - scaled.y * s,
        scaled.x * s + scaled.y * c,
    );
    return rotated + slot.offset_scale.xy;
}

/// Sample texture from bindless array, or return fallback if NO_TEXTURE
fn sample_texture(slot: MaterialTextureSlot, base_uv: vec2<f32>, fallback: vec4<f32>) -> vec4<f32> {
    if slot.texture_index == NO_TEXTURE {
        return fallback;
    }
    let uv = select_uv(slot, base_uv);
    return textureSample(scene_textures[slot.texture_index], scene_samplers[slot.texture_index], uv);
}

fn resolve_specular_f0(
    material: GpuMaterial,
    material_tex: MaterialTextureData,
    albedo: vec3<f32>,
    metallic: f32,
    uv: vec2<f32>,
) -> vec3<f32> {
    if material.workflow == MATERIAL_WORKFLOW_SPECULAR {
        let specular_color = sample_texture(material_tex.specular_color, uv, vec4<f32>(1.0)).rgb;
        let specular_weight = sample_texture(material_tex.specular_weight, uv, vec4<f32>(1.0)).a;
        let ior = max(material.roughness_metallic.z, 1.0);
        let dielectric_f0 = pow((ior - 1.0) / (ior + 1.0), 2.0);
        return material.roughness_metallic.w * specular_weight * specular_color * dielectric_f0;
    }

    // Metallic workflow: F0 = mix(0.04, albedo, metallic)
    return clamp(
        mix(vec3<f32>(0.04), albedo, metallic),
        vec3<f32>(0.0),
        vec3<f32>(0.999),
    );
}

// ── Default PBR material evaluation ──────────────────────────────────────────

fn default_pbr_surface(material: GpuMaterial, material_tex: MaterialTextureData, input: VertexOutput) -> SurfaceData {
    let uv = input.tex_coords;
    let base_sample = sample_texture(material_tex.base_color, uv, vec4<f32>(1.0));
    let albedo = material.base_color * base_sample;
    let alpha = albedo.a;

    let N_geom = normalize(input.world_normal);

    var N: vec3<f32>;
    if (material.flags & FLAG_HAS_NORMAL_MAP) != 0u && material_tex.normal.texture_index != NO_TEXTURE {
        let T = normalize(input.world_tangent - dot(input.world_tangent, N_geom) * N_geom);
        let B = cross(N_geom, T) * input.bitangent_sign;
        var norm_ts = sample_texture(material_tex.normal, uv, vec4<f32>(0.5, 0.5, 1.0, 1.0)).rgb * 2.0 - 1.0;
        norm_ts = vec3<f32>(norm_ts.x * material_tex.params.x, norm_ts.y * material_tex.params.x, norm_ts.z);
        N = normalize(T * norm_ts.x + B * norm_ts.y + N_geom * norm_ts.z);
    } else {
        N = N_geom;
    }

    let orm_sample = sample_texture(material_tex.roughness_metallic, uv, vec4<f32>(1.0));
    let occlusion_sample = sample_texture(material_tex.occlusion, uv, vec4<f32>(1.0));
    let emissive_sample = sample_texture(material_tex.emissive, uv, vec4<f32>(1.0));

    var ao: f32 = 1.0 + (occlusion_sample.r - 1.0) * material_tex.params.y;
    var roughness: f32 = clamp(material.roughness_metallic.x * orm_sample.g, 0.045, 1.0);
    var metallic: f32 = clamp(material.roughness_metallic.y * orm_sample.b, 0.0, 1.0);
    var specular_f0: vec3<f32> = resolve_specular_f0(material, material_tex, albedo.rgb, metallic, uv);
    var emissive: vec3<f32> = material.emissive.rgb * material.emissive.w * emissive_sample.rgb;

    return SurfaceData(albedo, N, ao, roughness, metallic, specular_f0, emissive, alpha,
                       0u, vec3<f32>(0.0), 0.0, 0.0, 0.0, 0.0);
}

// ── Radiant surface evaluation (tier-2 template entry point) ─────────────────
//
// NOTE: Individual local variables are kept in scope here (not `s.field`)
// for backward compatibility with graph-generated WGSL snippets that
// reference `emissive`, `roughness`, etc. as bare identifiers.
// Template overrides use `default_pbr_surface()` + `s.field` access.

fn radiant_eval_surface(material: GpuMaterial, material_tex: MaterialTextureData, input: VertexOutput) -> SurfaceData {
    var s = default_pbr_surface(material, material_tex, input);

    // Unpack for graph snippet access (local vars visible in the override block)
    var albedo: vec4<f32> = s.albedo;
    var N: vec3<f32> = s.normal;
    var ao: f32 = s.ao;
    var roughness: f32 = s.roughness;
    var metallic: f32 = s.metallic;
    var specular_f0: vec3<f32> = s.specular_f0;
    var emissive: vec3<f32> = s.emissive;
    var alpha: f32 = s.alpha;
    var surface_flags: u32 = s.flags;
    var subsurface_color: vec3<f32> = s.subsurface_color;
    var subsurface_radius: f32 = s.subsurface_radius;
    var roughness_aniso_x: f32 = s.roughness_aniso_x;
    var roughness_aniso_y: f32 = s.roughness_aniso_y;
    var aniso_rotation: f32 = s.aniso_rotation;

    // Radiant override point: graph-generated WGSL replaces this section to
    // override any SurfaceData field. When no graph is present the passthrough
    // below is used (the default PBR result).
    // RADIANT_OVERRIDE_SURFACE
    // RADIANT_OVERRIDE_END

    // Repack — fields that were not touched by the graph keep their default values.
    s.albedo = albedo;
    s.normal = N;
    s.ao = ao;
    s.roughness = roughness;
    s.metallic = metallic;
    s.specular_f0 = specular_f0;
    s.emissive = emissive;
    s.alpha = alpha;
    s.flags = surface_flags;
    s.subsurface_color = subsurface_color;
    s.subsurface_radius = subsurface_radius;
    s.roughness_aniso_x = roughness_aniso_x;
    s.roughness_aniso_y = roughness_aniso_y;
    s.aniso_rotation = aniso_rotation;

    return s;
}

fn compute_velocity(input: VertexOutput) -> vec2<f32> {
    let prev_ndc = input.prev_clip_position.xy / input.prev_clip_position.w;
    let prev_pixel_x = (prev_ndc.x * 0.5 + 0.5) * globals.screen_width;
    let prev_pixel_y = (0.5 - prev_ndc.y * 0.5) * globals.screen_height;
    let prev_pixel = vec2<f32>(prev_pixel_x, prev_pixel_y);
    return input.clip_position.xy - prev_pixel;
}

@fragment
fn fs_main(input: VertexOutput) -> GBufferOutput {
    let material = materials[input.material_id];
    let material_tex = material_textures[input.material_id];

    // DEBUG MODE 1: Show UVs as colors
    if globals.debug_mode == 1u {
        let uv = input.tex_coords;
        return GBufferOutput(
            vec4<f32>(uv.x, uv.y, 0.0, 1.0),
            vec4<f32>(0.0, 0.0, 1.0, 0.0),
            vec4<f32>(0.0),
            vec4<f32>(0.0),
            vec2<f32>(0.0),
            vec4<f32>(0.0),
            vec4<f32>(0.0),
            compute_velocity(input)
        );
    }

    // DEBUG MODE 2: Show texture sample directly
    if globals.debug_mode == 2u {
        let base_sample = sample_texture(material_tex.base_color, input.tex_coords, vec4<f32>(1.0));
        return GBufferOutput(
            vec4<f32>(base_sample.rgb, 1.0),
            vec4<f32>(0.0, 0.0, 1.0, 0.0),
            vec4<f32>(0.0),
            vec4<f32>(0.0),
            vec2<f32>(0.0),
            vec4<f32>(0.0),
            vec4<f32>(0.0),
            compute_velocity(input)
        );
    }

    // DEBUG MODE 3: Geometry normals only (skip normal mapping)
    if globals.debug_mode == 3u {
        let uv = input.tex_coords;
        let base_sample = sample_texture(material_tex.base_color, uv, vec4<f32>(1.0));
        let albedo = material.base_color * base_sample;
        let N_geom = normalize(input.world_normal);
        let orm_sample = sample_texture(material_tex.roughness_metallic, uv, vec4<f32>(1.0));
        let roughness = clamp(material.roughness_metallic.x * orm_sample.g, 0.045, 1.0);
        let metallic = clamp(material.roughness_metallic.y * orm_sample.b, 0.0, 1.0);
        return GBufferOutput(
            vec4<f32>(albedo.rgb, albedo.a),
            vec4<f32>(N_geom, 0.0),
            vec4<f32>(1.0, roughness, metallic, 0.0),
            vec4<f32>(0.0),
            vec2<f32>(0.0),
            vec4<f32>(0.0),
            vec4<f32>(0.0),
            compute_velocity(input)
        );
    }

    let surface = radiant_eval_surface(material, material_tex, input);

    // Alpha test
    if surface.alpha <= 0.001 { discard; }
    if surface.alpha < material_tex.params.z { discard; }

    var out: GBufferOutput;
    out.albedo = vec4<f32>(surface.albedo.rgb, surface.alpha);
    out.normal = vec4<f32>(surface.normal, surface.specular_f0.r);
    out.orm = vec4<f32>(surface.ao, surface.roughness, surface.metallic, surface.specular_f0.g);
    out.emissive = vec4<f32>(surface.emissive, surface.specular_f0.b);
    out.lightmap_uv = input.lightmap_uv;
    out.sss = vec4<f32>(surface.subsurface_color, surface.subsurface_radius);
    out.extra = vec4<f32>(surface.roughness_aniso_x, surface.roughness_aniso_y,
                          surface.aniso_rotation, bitcast<f32>(surface.flags));
    out.velocity = compute_velocity(input);
    return out;
}
